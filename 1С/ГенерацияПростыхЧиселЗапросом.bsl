// На MSSQL генерация 10^7 чисел выполняется за ~17 мин.
// Требуется 20 платформа или выше
// 1) в первом запросе генерируем числовую последовательность
// 2) Во втором запросе генерируем простые числа из этой числовой последовательности

ВЫБРАТЬ
	Числа.Число КАК Число
ПОМЕСТИТЬ Числа
ИЗ
	(ВЫБРАТЬ
		Числа.Число КАК Число,
		Числа.Число - 10 * ЦЕЛ(Числа.Число / 10) КАК ПоследняяЦифра
	ИЗ
		(ВЫБРАТЬ РАЗЛИЧНЫЕ
			ЕСТЬNULL(T.tt * 1000000, 0) + ЕСТЬNULL(U.uu * 100000, 0) + ЕСТЬNULL(V.vv * 10000, 0) + ЕСТЬNULL(W.ww * 1000, 0) + ЕСТЬNULL(X.xx * 100, 0) + ЕСТЬNULL(Y.yy * 10, 0) + Z.zz + 2 КАК Число
		ИЗ
			(ВЫБРАТЬ 0 КАК zz ОБЪЕДИНИТЬ ВЫБРАТЬ 1 ОБЪЕДИНИТЬ ВЫБРАТЬ 2 ОБЪЕДИНИТЬ ВЫБРАТЬ 3 ОБЪЕДИНИТЬ ВЫБРАТЬ 4 ОБЪЕДИНИТЬ ВЫБРАТЬ 5 ОБЪЕДИНИТЬ ВЫБРАТЬ 6 ОБЪЕДИНИТЬ ВЫБРАТЬ 7 ОБЪЕДИНИТЬ ВЫБРАТЬ 8 ОБЪЕДИНИТЬ ВЫБРАТЬ 9)
			КАК Z
			ПОЛНОЕ СОЕДИНЕНИЕ (
				ВЫБРАТЬ Y.yy КАК yy ИЗ 
				(ВЫБРАТЬ 0 КАК yy ОБЪЕДИНИТЬ ВЫБРАТЬ 1 ОБЪЕДИНИТЬ ВЫБРАТЬ 2 ОБЪЕДИНИТЬ ВЫБРАТЬ 3  ОБЪЕДИНИТЬ ВЫБРАТЬ 4 ОБЪЕДИНИТЬ ВЫБРАТЬ 5	ОБЪЕДИНИТЬ ВЫБРАТЬ 6 ОБЪЕДИНИТЬ	ВЫБРАТЬ 7 ОБЪЕДИНИТЬ ВЫБРАТЬ 8 ОБЪЕДИНИТЬ ВЫБРАТЬ 9) КАК Y
				ГДЕ &РазмерПоследовательности > 10)
			КАК Y ПО (ИСТИНА)
			ПОЛНОЕ СОЕДИНЕНИЕ (
				ВЫБРАТЬ X.xx КАК xx ИЗ 
				(ВЫБРАТЬ 0 КАК xx ОБЪЕДИНИТЬ ВЫБРАТЬ 1 ОБЪЕДИНИТЬ ВЫБРАТЬ 2 ОБЪЕДИНИТЬ ВЫБРАТЬ 3  ОБЪЕДИНИТЬ ВЫБРАТЬ 4 ОБЪЕДИНИТЬ ВЫБРАТЬ 5	ОБЪЕДИНИТЬ ВЫБРАТЬ 6 ОБЪЕДИНИТЬ	ВЫБРАТЬ 7 ОБЪЕДИНИТЬ ВЫБРАТЬ 8 ОБЪЕДИНИТЬ ВЫБРАТЬ 9) КАК X
				ГДЕ &РазмерПоследовательности > 100)
			КАК X ПО (ИСТИНА)          
			ПОЛНОЕ СОЕДИНЕНИЕ (
				ВЫБРАТЬ W.ww КАК ww ИЗ 
				(ВЫБРАТЬ 0 КАК ww ОБЪЕДИНИТЬ ВЫБРАТЬ 1 ОБЪЕДИНИТЬ ВЫБРАТЬ 2 ОБЪЕДИНИТЬ ВЫБРАТЬ 3  ОБЪЕДИНИТЬ ВЫБРАТЬ 4 ОБЪЕДИНИТЬ ВЫБРАТЬ 5	ОБЪЕДИНИТЬ ВЫБРАТЬ 6 ОБЪЕДИНИТЬ	ВЫБРАТЬ 7 ОБЪЕДИНИТЬ ВЫБРАТЬ 8 ОБЪЕДИНИТЬ ВЫБРАТЬ 9) КАК W
				ГДЕ &РазмерПоследовательности > 1000)
			КАК W ПО (ИСТИНА)           
			ПОЛНОЕ СОЕДИНЕНИЕ (
				ВЫБРАТЬ V.vv КАК vv ИЗ 
				(ВЫБРАТЬ 0 КАК vv ОБЪЕДИНИТЬ ВЫБРАТЬ 1 ОБЪЕДИНИТЬ ВЫБРАТЬ 2 ОБЪЕДИНИТЬ ВЫБРАТЬ 3  ОБЪЕДИНИТЬ ВЫБРАТЬ 4 ОБЪЕДИНИТЬ ВЫБРАТЬ 5	ОБЪЕДИНИТЬ ВЫБРАТЬ 6 ОБЪЕДИНИТЬ	ВЫБРАТЬ 7 ОБЪЕДИНИТЬ ВЫБРАТЬ 8 ОБЪЕДИНИТЬ ВЫБРАТЬ 9) КАК V
				ГДЕ &РазмерПоследовательности > 10000)
			КАК V ПО (ИСТИНА)         
			ПОЛНОЕ СОЕДИНЕНИЕ (
				ВЫБРАТЬ U.uu КАК uu ИЗ 
				(ВЫБРАТЬ 0 КАК uu ОБЪЕДИНИТЬ ВЫБРАТЬ 1 ОБЪЕДИНИТЬ ВЫБРАТЬ 2 ОБЪЕДИНИТЬ ВЫБРАТЬ 3  ОБЪЕДИНИТЬ ВЫБРАТЬ 4 ОБЪЕДИНИТЬ ВЫБРАТЬ 5	ОБЪЕДИНИТЬ ВЫБРАТЬ 6 ОБЪЕДИНИТЬ	ВЫБРАТЬ 7 ОБЪЕДИНИТЬ ВЫБРАТЬ 8 ОБЪЕДИНИТЬ ВЫБРАТЬ 9) КАК U
				ГДЕ &РазмерПоследовательности > 100000)
			КАК U ПО (ИСТИНА)       
			ПОЛНОЕ СОЕДИНЕНИЕ (
				ВЫБРАТЬ T.tt КАК tt ИЗ 
				(ВЫБРАТЬ 0 КАК tt ОБЪЕДИНИТЬ ВЫБРАТЬ 1 ОБЪЕДИНИТЬ ВЫБРАТЬ 2 ОБЪЕДИНИТЬ ВЫБРАТЬ 3  ОБЪЕДИНИТЬ ВЫБРАТЬ 4 ОБЪЕДИНИТЬ ВЫБРАТЬ 5	ОБЪЕДИНИТЬ ВЫБРАТЬ 6 ОБЪЕДИНИТЬ	ВЫБРАТЬ 7 ОБЪЕДИНИТЬ ВЫБРАТЬ 8 ОБЪЕДИНИТЬ ВЫБРАТЬ 9) КАК T
				ГДЕ &РазмерПоследовательности > 1000000) 
			КАК T ПО (ИСТИНА) 
		) КАК Числа
	ГДЕ
		Числа.Число <= &РазмерПоследовательности) КАК Числа
ГДЕ
	(Числа.ПоследняяЦифра = 1
		ИЛИ Числа.ПоследняяЦифра = 3
		ИЛИ Числа.ПоследняяЦифра = 7
		ИЛИ Числа.ПоследняяЦифра = 9)

ИНДЕКСИРОВАТЬ ПО
	Числа.Число
;
///////////////////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	КОЛИЧЕСТВО(ПростыеЧисла.ПростоеЧисло) КАК КоличествоПростыхЧисел
ИЗ
	(ВЫБРАТЬ
		Числа1.Число КАК ПростоеЧисло
	ИЗ
		Числа КАК Числа1
			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Числа КАК Числа2
			ПО (Числа2.Число <= SQRT(Числа1.Число))
				И (Числа1.Число - 2 * ЦЕЛ(Числа1.Число / 2) <> 0)
				И (Числа1.Число - 3 * ЦЕЛ(Числа1.Число / 3) <> 0)
				И (Числа1.Число - 5 * ЦЕЛ(Числа1.Число / 5) <> 0)
				И (Числа1.Число - 7 * ЦЕЛ(Числа1.Число / 7) <> 0)
	
	СГРУППИРОВАТЬ ПО
		Числа1.Число
	
	ИМЕЮЩИЕ
		МИНИМУМ(Числа1.Число - Числа2.Число * ЦЕЛ(Числа1.Число / Числа2.Число)) > 0
	
	ОБЪЕДИНИТЬ ВЫБРАТЬ 2 ГДЕ &РазмерПоследовательности >= 2
	ОБЪЕДИНИТЬ ВЫБРАТЬ 3 ГДЕ &РазмерПоследовательности >= 3
	ОБЪЕДИНИТЬ ВЫБРАТЬ 5 ГДЕ &РазмерПоследовательности >= 5
	ОБЪЕДИНИТЬ ВЫБРАТЬ 7 ГДЕ &РазмерПоследовательности >= 7

) КАК ПростыеЧисла
